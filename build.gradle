// Build script executed before the actual gradle build script to do any pre-setup
buildscript {

    dependencies {
        // The dependency management plugin changes Gradle’s handling of a pom’s exclusions so that they behave as they do in Maven.
        // https://spring.io/blog/2015/02/23/better-dependency-management-for-gradle
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    }
}

plugins {

    // core plugins
    id 'java'
    id 'eclipse'
    id 'maven'
    id 'idea'
    id 'jacoco'


    // Spring plugins
    id "org.springframework.boot" version "1.4.3.RELEASE"
    id "io.spring.dependency-management" version "0.6.0.RELEASE"
}

// Compilation settings
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {

    groupId					= 'com.mp.demo'
    artifactId				= 'ribboneureka.server'
    versionNumber			= '1.0.0'
    jacksonVersion 			= '2.9.0.pr2'
}

allprojects {
    apply plugin: 'application'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-netflix:1.2.4.RELEASE"
    }
}
repositories {
    mavenCentral()
}

dependencies {

    // Spring-Boot dependencies
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    // Spring-Cloud dependencies
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-config'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-eureka'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-ribbon'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-turbine'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard'

    // Jackson Dependencies
    compile group: 'com.fasterxml.jackson.module', 		name: 'jackson-module-parameter-names', version: "${project.ext['jacksonVersion']}", transitive: true
    compile group: 'com.fasterxml.jackson.datatype', 	name: 'jackson-datatype-jdk8', 			version: "${project.ext['jacksonVersion']}", transitive: true
    compile group: 'com.fasterxml.jackson.datatype', 	name: 'jackson-datatype-jsr310',		version: "${project.ext['jacksonVersion']}", transitive: true

    // Log4j2 Dependency for SLF4J and Asynchronous logging
    compile group: 'com.lmax', name: 'disruptor', version: '3.3.6'
}

configurations.all {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    exclude group: "ch.qos.logback", module: "logback-classic"
}

configurations {
    runtime.exclude group:"ch.qos.logback", module: "logback-classic"
}


jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/SearchBackendApplication**'])
        })
    }
}

jar {
    baseName = project.artifactId
    version = project.versionNumber
}

bootRun {
    // Add's support for passing system properties to the 'bootRun' task
    systemProperties = System.properties
}

run{
    mainClassName = "ribboneureka.server.SearchBackendApplication"
}